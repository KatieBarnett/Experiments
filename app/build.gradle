plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

static def computeVersionCode() {
    def initValue = 1
    def process = "git rev-list HEAD --count".execute()
    def gitVersion = process.text
    if (gitVersion == "") {
        return initValue
    } else {
        def versionCode = initValue + gitVersion.toInteger()
        return versionCode
    }
}

android {
    compileSdk compile_sdk_version
    
    defaultConfig {
        applicationId "dev.katiebarnett.experiments"
        minSdk min_sdk_version
        targetSdk compile_sdk_version
        versionCode computeVersionCode()
        versionName app_version_name

        // Required because of https://commonsware.com/blog/2020/10/14/android-studio-4p1-library-modules-version-code.html
        buildConfigField ('int', 'APP_VERSION_CODE', computeVersionCode().toString())
        buildConfigField 'String', 'APP_VERSION_NAME', "\"$app_version_name\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }
}

dependencies {
    
    implementation project(':core')

    implementation coreLibs
    implementation uiLibs
    implementation jetpackComposeLibs
    debugImplementation jetpackComposeLibsDebug
    implementation lifecycleLibs

    kapt coreLibsKapt
}